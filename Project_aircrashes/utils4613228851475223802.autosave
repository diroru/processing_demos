//TODO: font settings

void drawArcTextCentered(String theText, PVector v) {  
  drawArcTextCentered(theText, v.x, v.y);
}

void drawArcTextCentered(String theText, float x0, float y0) {
  float theW = textWidth(theText);
  float rad = PVector.dist(new PVector(x0, y0), center());
  float phi = getPhiFromSides(theW, rad);
  PVector v = new PVector(x0, y0);
  v.sub(center());
  v.rotate(phi*0.5);
  v.add(center());
  drawArcText(theText, v.x, v.y);
}

void drawArcText(String theText, PVector v) {
  drawArcText(theText, v.x, v.y);
}

void drawArcText(String theText, float x0, float y0) {
  float dx = x0 - width*0.5;
  float dy = y0 - height*0.5;
  float x = x0;
  float y = y0;
  float radius = sqrt(dx * dx + dy * dy);
  char[] letters = theText.toCharArray();
  for (int i = 0; i < letters.length; i++) {
    char letter = letters[i];
    drawTangentialText(letter, x, y);

    float characterWidth = textWidth(letter);
    //applying sine theorem
    float theta = 2f * asin(characterWidth / radius * 0.5f);
    PVector nextCoords = new PVector(x, y);
    nextCoords.add(new PVector(-width*0.5, -height*0.5));
    nextCoords.rotate(-theta);
    nextCoords.add(new PVector(width*0.5, height*0.5));
    x = nextCoords.x;
    y = nextCoords.y;
  }
}

void drawTangentialTextPolar(String theText, float theta, float radius) {
  float x = width * 0.5 + cos(theta) * radius;
  float y = height * 0.5 + sin(theta) * radius;
  drawTangentialText(theText + "", x, y);
}

void drawTangentialText(String theText, PVector v) {
  drawTangentialText(theText + "", v.x, v.y);
}

void drawTangentialText(char theText, float x, float y) {
  drawTangentialText(theText + "", x, y);
}

void drawTangentialText(String[] theTextTokens, color[] colors, float x, float y) {
  drawTangentialText(theTextTokens, colors, 255, x, y);
}

void drawTangentialText(String[] theTextTokens, color[] colors, float alpha, float x, float y) {
  float dx = x - width*0.5;
  float dy = y - height*0.5;
  float angle = atan2(dy, dx) - HALF_PI;

  pushMatrix();
  translate(x, y);
  rotate(angle);
  for (int i = 0; i < theTextTokens.length; i++) {
    fill(colors[i], alpha);
    text(theTextTokens[i], 0, 0);
    translate(textWidth(theTextTokens[i]), 0);
  }
  popMatrix();
}

void drawTangentialTextPolar(String[] theTextTokens, color[] colors, float alpha, float theta, float radius) {
  float x = width*0.5 + cos(theta) * radius;
  float y = height*0.5 + sin(theta) * radius;
  drawTangentialText(theTextTokens, colors, alpha, x, y);
}

void drawTangentialText(String theText, float x, float y) {
  float dx = x - width*0.5;
  float dy = y - height*0.5;
  float angle = atan2(dy, dx) - HALF_PI;

  pushMatrix();
  translate(x, y);
  rotate(angle);
  text(theText, 0, 0);
  popMatrix();
}

void drawRadialText(String s, PVector p, float angle, float angle2, float radius) {
  pushMatrix();
  translate(p.x, p.y);
  rotate(angle);
  translate(radius, 0);
  rotate(angle2);
  text(s, 0, 0);
  popMatrix();
}

PVector incrementRadially(PVector src, float delta) {
  PVector result = src.copy();
  result.sub(new PVector(width * 0.5, height * 0.5));
  float mag = result.mag();
  result.setMag(mag + delta);
  result.add(new PVector(width * 0.5, height * 0.5));
  return result;
}

PVector center() {
  return new PVector(width*0.5, height*0.5);
}

float getPhiFromSides(float base, float radius) {
  return asin(base * 0.5 / radius) * 2;
}

int signum(float f) {
  if (f >= 0) {
    return 1;
  }
  return -1;
}

void updateSequence(CrashFlight nextFlight) {
  updateSequence(nextFlight, 0f);
}

void updateSequence(CrashFlight nextFlight, float seekTime) {
  activeFlight = nextFlight;
  float[] nextProjecitonParams = getProjectionParams(activeFlight.myDatum);

  float nextDeltaLat = nextProjecitonParams[0];
  float nextDeltaLon = nextProjecitonParams[1];
  float nextDeltaLonPost = nextProjecitonParams[2] + HALF_PI;
  float nextMapScale = max(nextProjecitonParams[3], MIN_MAP_SCALE);

  float ddlon = nextDeltaLon - deltaLon;
  while (abs(ddlon) > PI) {
    ddlon -= signum(ddlon) * TWO_PI;
  }

  nextDeltaLon = deltaLon + ddlon;

  float ddlonp = nextDeltaLonPost - deltaLonPost;
  while (abs(ddlonp) > PI) {
    ddlonp -= signum(ddlonp) * PI;
  }
  if (abs(ddlonp) > HALF_PI) {
    ddlonp = ddlonp - signum(ddlonp) * PI;
  }

  nextDeltaLonPost = deltaLonPost + ddlonp;

  println("animating delta lat", degrees(deltaLat), " > ", degrees(nextDeltaLat));
  println("animating delta lon", degrees(deltaLon), " > ", degrees(nextDeltaLon));
  println("animating delta lon post", degrees(deltaLonPost), " > ", degrees(nextDeltaLonPost));
  println("animating scale", mapScale, " > ", nextMapScale);
  println(degrees(nextDeltaLon) - degrees(nextDeltaLonPost), degrees(nextDeltaLon) + degrees(nextDeltaLonPost));
  println("-----");

  float t = SEEK_DURATION; 
  float t_half = t/2f;
  float t_two_thirds = t/3f * 2f;
  float t_third = t/3f;
  float dt = t*0.15;
  float tSum = 0;

  //if (globalAniSequence.isEnded() || !globalAniSequence.isPlaying()) {
  Ani.killAll();
  globalAniSequence = new AniSequence(this);
  globalAniSequence.beginSequence();

  globalAniSequence.beginStep();
  //1. animate time, NB callback!
  globalAniSequence.add(Ani.to(this, t, "TIME", activeFlight.myDatum.normMoment, Ani.LINEAR));
  //2. zoom out
  globalAniSequence.add(Ani.to(this, t_third, 0, "mapScale", MAX_MAP_SCALE));
  //3. rotate
  globalAniSequence.add(Ani.to(this, t_third + dt*2, t_third - dt, "deltaLon", nextDeltaLon));
  globalAniSequence.add(Ani.to(this, t_third + dt*2, t_third - dt, "deltaLonPost", nextDeltaLonPost));
  globalAniSequence.add(Ani.to(this, t_third + dt*2, t_third - dt, "deltaLat", nextDeltaLat));
  globalAniSequence.endStep();
  globalAniSequence.beginStep();
  //4. zoom in
  globalAniSequence.add(Ani.to(this, t_third, 0, "mapScale", nextMapScale));
  globalAniSequence.endStep();
  //5. TODO: animate flight
  
  globalAniSequence.beginStep();
  globalAniSequence.add(Ani.to(this, FADE_DURATION, "FADE_TIME", 1));
  globalAniSequence.endStep();
  
  globalAniSequence.beginStep();
  globalAniSequence.add(Ani.to(this, TRAJECTORY_SHOW_DURATION, "TRAJECTORY_SHOW_TIME", 1));
  globalAniSequence.endStep();
  globalAniSequence.beginStep();
  globalAniSequence.add(Ani.to(this, FADE_DURATION, "CRASH_INFO_SHOW_TIME", 1, Ani.BOUNCE_OUT));
  globalAniSequence.endStep();
  
  globalAniSequence.beginStep();
  globalAniSequence.add(Ani.to(this, FADE_DURATION, CRASH_INFO_SHOW_DURATION, "FADE_TIME", 0, Ani.getDefaultEasing(), "onEnd:callbackAfterFlightShown"));
  globalAniSequence.endStep();
  

  globalAniSequence.endSequence();
  //println("seeking to", seekTime);
  globalAniSequence.start();
  //globalAniSequence.seek(seekTime);


  //} else {
  // println("not ended or is playing");   
  //}
}

void callbackAfterFlightShown() {
  switch(currentState) {
  case STATE_PLAY:
    TRAJECTORY_SHOW_TIME = 0;
    CRASH_INFO_SHOW_TIME = 0;
    activeFlight = getNextFlight(activeFlight);
    updateSequence(activeFlight);
    break;
  }
}

CrashFlight getNextFlight(CrashFlight theActiveFlight) {
  if (theActiveFlight == null) {
    return myFlights.get(0);
  }
  return theActiveFlight.nextFlight;
}


void setActiveFlightOld(CrashFlight theFlight) {
  activeFlight = theFlight;
  /*
  activeFlight.finished = false;
   activeFlight.pausable = false;
   activeFlight.displayAll = false;
   */
  float[] nextProjecitonParams = getProjectionParams(activeFlight.myDatum);

  //float nextDeltaLat = (nextProjecitonParams[0] + TWO_PI) % TWO_PI;
  //float nextDeltaLon = (nextProjecitonParams[1] + TWO_PI) % TWO_PI;
  //float nextDeltaLonPost = (nextProjecitonParams[2] + TWO_PI) % TWO_PI;
  float nextDeltaLat = nextProjecitonParams[0];
  float nextDeltaLon = nextProjecitonParams[1];
  float nextDeltaLonPost = nextProjecitonParams[2] + HALF_PI;
  float nextMapScale = constrain(nextProjecitonParams[3], MIN_MAP_SCALE, MAX_MAP_SCALE);


  float ddlon = nextDeltaLon - deltaLon;
  //println(degrees(ddlon));
  while (abs(ddlon) > PI) {
    ddlon -= signum(ddlon) * TWO_PI;
  }

  nextDeltaLon = deltaLon + ddlon;

  float ddlonp = nextDeltaLonPost - deltaLonPost;
  while (abs(ddlonp) > PI) {
    ddlonp -= signum(ddlonp) * PI;
  }
  if (abs(ddlonp) > HALF_PI) {
    ddlonp = ddlonp - signum(ddlonp) * PI;
  }

  nextDeltaLonPost = deltaLonPost + ddlonp;

  println("animating delta lat", degrees(deltaLat), " > ", degrees(nextDeltaLat));
  println("animating delta lon", degrees(deltaLon), " > ", degrees(nextDeltaLon));
  println("animating delta lon post", degrees(deltaLonPost), " > ", degrees(nextDeltaLonPost));
  println("animating scale", mapScale, " > ", max(nextProjecitonParams[3], MIN_MAP_SCALE));
  println(degrees(nextDeltaLon) - degrees(nextDeltaLonPost), degrees(nextDeltaLon) + degrees(nextDeltaLonPost));
  println("-----");
  Ani.to(this, 5.0, "deltaLat", nextDeltaLat);
  Ani.to(this, 5.0, "deltaLon", nextDeltaLon);
  Ani.to(this, 5.0, "deltaLonPost", nextDeltaLonPost);
  Ani.to(this, 5.0, "mapScale", max(nextMapScale, MIN_MAP_SCALE));
}
